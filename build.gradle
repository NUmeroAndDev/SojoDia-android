enum VersionSuffix {
    ALPHA(0, "alpha"), BETA(1, "beta"), PRODUCT(9, "")

    private final int code
    private final String name

    VersionSuffix(int code, String name) {
        this.code = code
        this.name = name
    }

    int getCode() {
        code
    }

    String getName() {
        name
    }
}

ext {
    def suffix = VersionSuffix.PRODUCT

    def appVersionMajor = 9
    def appVersionMinor = 1
    def appVersionPatch = 0
    def appVersionBuild = 0

    appVersionCode = appVersionMajor * 100000 + appVersionMinor * 10000 + appVersionPatch * 1000 + suffix.code * 100 + appVersionBuild
    appVersionName = suffix == VersionSuffix.PRODUCT ?
            "${appVersionMajor}.${appVersionMinor}.${appVersionPatch}" :
            "${appVersionMajor}.${appVersionMinor}.${appVersionPatch}-${suffix.name} ${appVersionBuild}"
}

buildscript {

    ext {

        versions = [
                compileSdk: 30,
                minSdk    : 23,
                targetSdk : 30,

                kotlin    : "1.4.32",

                compose   : "1.0.0-beta07",

                lifecycle : "2.4.0-alpha01",
                room      : "2.3.0-alpha03",

                okhttp    : "4.9.1",
                retrofit  : "2.9.0",
        ]

        libs = [
                kotlin     : [
                        plugin    : "org.jetbrains.kotlin:kotlin-gradle-plugin:$versions.kotlin",
                        stdlib    : "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$versions.kotlin",
                        coroutines: "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.4.2"
                ],

                androidx   : [
                        appcompat       : "androidx.appcompat:appcompat:1.2.0",
                        activity        : "androidx.activity:activity-compose:1.3.0-alpha08",
                        fragment        : "androidx.fragment:fragment-ktx:1.3.0-beta02",
                        core            : "androidx.core:core-ktx:1.5.0-alpha05",
                        recyclerview    : "androidx.recyclerview:recyclerview:1.2.0-beta01",
                        viewpager2      : "androidx.viewpager2:viewpager2:1.1.0-alpha01",
                        constraintlayout: "androidx.constraintlayout:constraintlayout:2.0.1",

                        lifecycle       : [
                                livedata: "androidx.lifecycle:lifecycle-livedata-ktx:$versions.lifecycle",
                                runtime : "androidx.lifecycle:lifecycle-runtime:$versions.lifecycle",
                                common  : "androidx.lifecycle:lifecycle-common-java8:$versions.lifecycle"
                        ],

                        room            : [
                                runtime : "androidx.room:room-runtime:$versions.room",
                                compiler: "androidx.room:room-compiler:$versions.room"
                        ],

                        compose         : [
                                ui        : "androidx.compose.ui:ui:$versions.compose",
                                material  : "androidx.compose.material:material:$versions.compose",
                                ui_tooling: "androidx.compose.ui:ui-tooling:$versions.compose"
                        ],

                        navigation      : [
                                compose: "androidx.navigation:navigation-compose:2.4.0-alpha01"
                        ],

                        work            : "androidx.work:work-runtime-ktx:2.4.0",

                        test            : "androidx.test.ext:junit:1.1.2"
                ],

                material   : "com.google.android.material:material:1.3.0-alpha03",

                insetter   : "dev.chrisbanes:insetter-ktx:0.3.1",

                okhttp     : [
                        core   : "com.squareup.okhttp3:okhttp:$versions.okhttp",
                        logging: "com.squareup.okhttp3:logging-interceptor:$versions.okhttp",
                        mock   : "com.squareup.okhttp3:mockwebserver:$versions.okhttp"
                ],
                retrofit   : [
                        core : "com.squareup.retrofit2:retrofit:$versions.retrofit",
                        moshi: "com.squareup.retrofit2:converter-moshi:$versions.retrofit",
                ],
                kotshi     : [
                        api     : "se.ansman.kotshi:api:1.0.6",
                        compiler: "se.ansman.kotshi:compiler:1.0.6"
                ],

                play       : "com.google.android.play:core:1.8.3",

                licenses   : "com.google.android.gms:play-services-oss-licenses:17.0.0",

                junit      : "junit:junit:4.13.1",
                robolectric: "org.robolectric:robolectric:4.4",
                espresso   : "androidx.test.espresso:espresso-core:3.3.0"
        ]
    }

    repositories {
        jcenter()
        google()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:7.4.0-alpha02'

        classpath libs.kotlin.plugin

        classpath 'com.google.android.gms:oss-licenses-plugin:0.10.4'
    }
}

allprojects {
    repositories {
        jcenter()
        google()
    }
}
