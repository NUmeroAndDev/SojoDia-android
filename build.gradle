enum VersionSuffix {
    ALPHA(0, "alpha"), BETA(1, "beta"), PRODUCT(9, "")

    private final int code
    private final String name

    VersionSuffix(int code, String name) {
        this.code = code
        this.name = name
    }

    int getCode() {
        code
    }

    String getName() {
        name
    }
}

ext {
    def suffix = VersionSuffix.PRODUCT

    def appVersionMajor = 8
    def appVersionMinor = 1
    def appVersionPatch = 0
    def appVersionBuild = 0
    /**
     * device number
     * 0 = phone
     * 1 = wear
     */
    def appTargetDevice = 0

    appVersionCode = appVersionMajor * 100000 + appVersionMinor * 10000 + appVersionPatch * 1000 + suffix.code * 100 + appVersionBuild * 10 + appTargetDevice
    appVersionName = suffix == VersionSuffix.PRODUCT ?
            "${appVersionMajor}.${appVersionMinor}.${appVersionPatch}" :
            "${appVersionMajor}.${appVersionMinor}.${appVersionPatch}-${suffix.name} ${appVersionBuild}"
}

buildscript {

    ext {
        kotlinVersion = '1.3.11'

        appCompatVersion = '1.1.0-alpha01'
        ktxVersion = '1.0.1'
        materialVersion = '1.1.0-alpha02'
        recyclerViewVersion = '1.1.0-alpha01'
        constraintLayoutVersion = '2.0.0-alpha2'
        preferenceVersion = '1.1.0-alpha01'
        roomVersion = '2.0.0'

        okhttpVersion = '3.9.0'
        kotshiVersion = '1.0.5'
        retrofitVersion = '2.4.0'
        rxJavaVersion = '2.2.3'
        rxAndroidVersion = '2.1.0'
        rxKotlinVersion = '2.3.0'

        junitVersion = '4.12'
        robolectricVersion = '4.0.2'
        testRunnerVersion = '1.1.0'
        testJunitVersion = '1.0.0'
        espressoVersion = '3.1.0'
    }

    repositories {
        jcenter()
        google()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:3.4.0-beta03'

        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"

        classpath 'com.cookpad.android.licensetools:license-tools-plugin:1.5.0'
    }
}

allprojects {
    repositories {
        jcenter()
        google()
    }
}
