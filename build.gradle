enum VersionSuffix {
    ALPHA(0, "alpha"), BETA(1, "beta"), PRODUCT(9, "")

    private final int code
    private final String name

    VersionSuffix(int code, String name) {
        this.code = code
        this.name = name
    }

    int getCode() {
        code
    }

    String getName() {
        name
    }
}

ext {
    def suffix = VersionSuffix.PRODUCT

    def appVersionMajor = 9
    def appVersionMinor = 0
    def appVersionPatch = 1
    def appVersionBuild = 0

    appVersionCode = appVersionMajor * 100000 + appVersionMinor * 10000 + appVersionPatch * 1000 + suffix.code * 100 + appVersionBuild
    appVersionName = suffix == VersionSuffix.PRODUCT ?
            "${appVersionMajor}.${appVersionMinor}.${appVersionPatch}" :
            "${appVersionMajor}.${appVersionMinor}.${appVersionPatch}-${suffix.name} ${appVersionBuild}"
}

buildscript {

    ext {

        versions = [
                compileSdk: 30,
                minSdk    : 23,
                targetSdk : 30
        ]

        kotlinAndroid = [
                plugin    : "org.jetbrains.kotlin:kotlin-gradle-plugin:1.4.10",
                stdlib    : "org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.4.10",
                coroutines: "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.3.9"
        ]

        androidx = [
                appcompat       : "androidx.appcompat:appcompat:1.2.0",
                activity        : "androidx.activity:activity-ktx:1.2.0-alpha08",
                fragment        : "androidx.fragment:fragment-ktx:1.3.0-alpha08",
                core            : "androidx.core:core-ktx:1.5.0-alpha03",
                recyclerview    : "androidx.recyclerview:recyclerview:1.2.0-alpha05",
                viewpager2      : "androidx.viewpager2:viewpager2:1.1.0-alpha01",
                constraintlayout: "androidx.constraintlayout:constraintlayout:2.0.1",

                lifecycle       : [
                        livedata: "androidx.lifecycle:lifecycle-livedata-ktx:2.3.0-alpha07",
                        runtime : "androidx.lifecycle:lifecycle-runtime:2.3.0-alpha07"
                ],

                room            : [
                        runtime : "androidx.room:room-runtime:2.3.0-alpha02",
                        compiler: "androidx.room:room-compiler:2.3.0-alpha02"
                ],

                work            : "androidx.work:work-runtime-ktx:2.4.0",

                test            : "androidx.test.ext:junit:1.1.2"
        ]

        material = "com.google.android.material:material:1.3.0-alpha02"

        insetter = "dev.chrisbanes:insetter-ktx:0.3.1"

        okhttp = [
                core   : "com.squareup.okhttp3:okhttp:4.9.0",
                logging: "com.squareup.okhttp3:logging-interceptor:4.9.0",
                mock   : "com.squareup.okhttp3:mockwebserver:4.9.0"
        ]
        retrofit = [
                core : "com.squareup.retrofit2:retrofit:2.9.0",
                moshi: "com.squareup.retrofit2:converter-moshi:2.9.0",
        ]
        kotshi = [
                api     : "se.ansman.kotshi:api:1.0.6",
                compiler: "se.ansman.kotshi:compiler:1.0.6"
        ]

        play = "com.google.android.play:core:1.8.0"

        licenses = "com.google.android.gms:play-services-oss-licenses:17.0.0"

        junit = "junit:junit:4.13"
        robolectric = "org.robolectric:robolectric:4.3.1"
        espresso = "androidx.test.espresso:espresso-core:3.3.0"
    }

    repositories {
        jcenter()
        google()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:4.2.0-alpha12'

        classpath kotlinAndroid.plugin

        classpath 'com.google.android.gms:oss-licenses-plugin:0.10.2'
    }
}

allprojects {
    repositories {
        jcenter()
        google()
    }
}
