enum VersionSuffix {
    ALPHA(0, "alpha"), BETA(1, "beta"), PRODUCT(9, "")

    private final int code
    private final String name

    VersionSuffix(int code, String name) {
        this.code = code
        this.name = name
    }

    int getCode() {
        code
    }

    String getName() {
        name
    }
}

ext {
    def suffix = VersionSuffix.ALPHA

    def appVersionMajor = 9
    def appVersionMinor = 0
    def appVersionPatch = 0
    def appVersionBuild = 0

    appVersionCode = appVersionMajor * 100000 + appVersionMinor * 10000 + appVersionPatch * 1000 + suffix.code * 100 + appVersionBuild
    appVersionName = suffix == VersionSuffix.PRODUCT ?
            "${appVersionMajor}.${appVersionMinor}.${appVersionPatch}" :
            "${appVersionMajor}.${appVersionMinor}.${appVersionPatch}-${suffix.name} ${appVersionBuild}"
}

buildscript {

    ext {

        versions = [
                compileSdk: 29,
                minSdk    : 23,
                targetSdk : 29
        ]

        kotlinVersion = '1.3.50'
        coroutinesVersion = '1.0.1'

        appCompatVersion = '1.1.0'
        activityVersion = '1.1.0-alpha03'
        fragmentVersion = '1.2.0-alpha03'
        ktxVersion = '1.2.0-alpha04'
        materialVersion = '1.1.0-alpha10'
        recyclerViewVersion = '1.1.0-beta04'
        constraintLayoutVersion = '2.0.0-beta1'
        roomVersion = '2.1.0'
        lifecycleVersion = '2.2.0-alpha02'
        workVersion = '2.0.1'

        okhttpVersion = '3.13.1'
        kotshiVersion = '1.0.6'
        retrofitVersion = '2.5.0'

        playCoreVersion = '1.6.1'

        junitVersion = '4.12'
        robolectricVersion = '4.3'
        testRunnerVersion = '1.1.0'
        testJunitVersion = '1.1.1'
        espressoVersion = '3.1.0'
    }

    repositories {
        jcenter()
        google()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:3.5.0'

        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"

        classpath 'com.cookpad.android.licensetools:license-tools-plugin:1.5.0'
    }
}

allprojects {
    repositories {
        jcenter()
        google()
    }
}
